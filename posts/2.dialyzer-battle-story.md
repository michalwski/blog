A battle story with dialyzer.

Here's the code: https://github.com/esl/escalus/tree/e26941110aa50e0eae555eb8dcb83f6083d48351

The problem is in `escalus_session` module.
Dialyzer output for this can be found on travis: https://travis-ci.org/esl/escalus/jobs/165476937#L219

Basically it emits following warnings:

```
escalus_session.erl:273: The pattern {{'xmlstreamstart', _, _}, 'escalus_ws', 'false'} can never match the type {#xmlel{name::'undefined' | binary(),attrs::[{_,_}],children::[{_,_} | {_,_,_,_}]},_,_}
escalus_session.erl:276: The pattern {{'xmlstreamstart', _, _}, _, _} can never match the type {#xmlel{name::'undefined' | binary(),attrs::[{_,_}],children::[{_,_} | {_,_,_,_}]},_,_}
escalus_session.erl:352: Matching of pattern {'xmlstreamstart', Name, Attrs} tagged with a record name violates the declared type of #xmlel{name::<<_:32>>,attrs::[{_,_}],children::[{_,_} | {_,_,_,_}]}
```

The first 2 (line 273, and 276) are in function `escalus_session:assert_stream_start`.
Dialyzer says that the pattern in line 276 can never match the type `#xmlel` but in fact
this case clause is matched. In MongooseIM there are tests for that, f.e component_SUITE
group xep0114_ws uses this exact clause.

Let's dive in to the code. The function assert_stream_start is called in `escalus_session:stream_start`
in line 75.
```erlang
assert_stream_start(StreamStartRep, Transport, IsLegacy),
```

Following the first argument `StreamStartRep` we can see it was assinged one line above.

```
StreamStartRep = escalus_connection:get_stanza(Conn, wait_for_stream),
```

We can suspect that the function spec for `escalus_connection:get_stanza` may not be correct and doesn't allow the
problematic #xmlstreamstart.
The function spec looks like this:
```erlang
-spec get_stanza(client(), any()) -> exml_stream:element().
```

Now, let's look how the exml_stream:element() is defined:
```erlang
-type start() :: #xmlstreamstart{}.
-type stop() :: #xmlstreamend{}.
-type element() :: exml:element() | start() | stop().
```

Based on that we can assume that the possible returned type from `escalus_connection:get_stanza`
is `#xmlstreamstart`. Exactly this record which is not accepted by dialyser in
escalus_session in line 276. How is that even possible.

We need to look deeper and see what happens inside the `escalus_connection:get_stanza`.
```erlang
-spec get_stanza(client(), any()) -> exml_stream:element().
get_stanza(Conn, Name) ->
    get_stanza(Conn, Name, ?TIMEOUT).

-spec get_stanza(client(), any(), timeout()) -> exml_stream:element().
get_stanza(#client{rcv_pid = Pid, jid = Jid}, Name, Timeout) ->
    receive
        {stanza, #client{rcv_pid = Pid}, Stanza} ->
            escalus_ct:log_stanza(Jid, in, Stanza),
            Stanza
    after Timeout ->
            throw({timeout, Name})
    end.
```

What is returned from the function is the `Stanza` received from the process
holding connection to the server. This looks correct, isn't it.
There is one more function called before returning the stanza to the caller.
Let's check what types can `escalus_ct:log_stanza` take.

```erlang
-spec log_stanza(undefined | binary(), in | out, exml:element()) -> ok.
```

As we can see the `Stanza` according to the spec can be of type `exml:element()` which is only:
```erlang
-type element() :: #xmlel{}.
```

And here we have the problem. Dialyzer sees that the function `escalus_ct:log_stanza` can take
only `exml:element()` and not whole `exml_stream:element()`.
Based on that it assumes that the only retuned type from `escalus_connection:get_stanza` is
`exml:element()`. Why is that? Simply because other parts of `exml_stream:element` are not allowed
by the type spec of function `escalus_ct:log_stanza`.

Conclusion:
1. While checking why dialyzer emits warnings it's not enough to look only at returned types of
function which are called before the problematic line. We need to look at input parameters'
type as well. Dialyzer can narrow the retunred type based on the spec of functions which are called
before returning the result.

2. If you want to use dialyzer, and you probably do :) Add it to your CI so the build fails if dialyzer fails.
We would not have this issue if the dialyzer fail our build when the `escalus_ct:log_stanza` function
was introduced.

Special thanks goes to @chrzaszcz and @mentels for helping with this warning.


